plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id "com.moowork.node" version "1.3.1"
}

apply plugin: "com.moowork.node"

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}

node {
    workDir = file("./app")
    nodeModulesDir = file("./app")
}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('./app/node_modules/.bin/webpack')
    args = [
            '--mode', 'development',
            '--entry', './src/index.js',
            '-o', '../src/main/resources/static/caverna.js',
            '--config', './webpack.config.js'
    ]
    execOverrides {
        it.workingDir = file('./app')
    }
}

processResources.dependsOn buildReactApp
clean.delete << file('./app/node_modules')
clean.delete << file('src/main/resources/static')
build.dependsOn buildReactApp
